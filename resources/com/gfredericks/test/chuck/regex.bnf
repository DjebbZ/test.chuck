Regex = Expr * PartialQELiteral ?

Expr = BaseExpr | ParentheticalExpr
ParentheticalExpr = '(' Expr * ')'

BaseExpr = LiteralChar | CharExpr | Anchor | Suffix | Pipe | QELiteral
Pipe = '|'
Suffix = '?' | '+' | '*' | CurlySuffix
CurlySuffix = '{' #"\d+" ( ',' ( #"\d+" ) ? ) ? '}'
Anchor = '^' | '$'
LiteralChar = PlainChar | EscapedChar | ControlChar
EscapedChar = '\\' AnyCharAtAll

(* What even are these? *)
ControlChar = '\\c' AnyCharAtAll

AnyCharAtAll = (#"." | CharThatInstaparseDotExcludes)
CharThatInstaparseDotExcludes = #"[\u0000\u000A\u000D\u005c]"

PartialQELiteral = '\\Q' NotSlashE *
QELiteral = '\\Q' NotSlashE * '\\E'
NotSlashE = #"[^\\]|\\[^E]"

PlainChar = #"[^.|\\+*$^\[(){]" | CharThatInstaparseDotExcludes
CharExpr = Dot | SpecialCharClass | ExplicitCharClass
Dot = '.'
(* probably missing something here *)
SpecialCharClass = #"\\[wWsS]"
ExplicitCharClass = '[' (#"[^\]]") * ']'